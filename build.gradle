// Top-level build file where you can add configuration options common to all sub-projects/modules.


buildscript {
    ext.applyRootProjectProperties = {
        Properties local_properties = new Properties()
        File localGradlePropertiesFile = project.rootProject.file('local.properties')
        println localGradlePropertiesFile.path
        if (localGradlePropertiesFile.exists()) {
            local_properties.load(localGradlePropertiesFile.newDataInputStream())
        }
        Map<String, String> localProperties = new HashMap<>()
        local_properties.each { key, value ->
            localProperties.put(key, value)
        }
        return localProperties
    }
    ext.localProperties = applyRootProjectProperties()
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${AGP_VERSION}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects { Project project ->
    if (!localProperties.isEmpty()) {
        localProperties.forEach { key, value ->
            project.ext[key] = value
        }
    }
    apply from: rootProject.file('./gradle/checkStyle.gradle')
}

ext {
    versions = [
            'compileSdk': 29,
            'minSdk'    : 14,
            'targetSdk' : 28,
            'support'   : '27.1.1'
    ]
    sample = false
}
